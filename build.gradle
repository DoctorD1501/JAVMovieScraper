plugins {
	id "us.kirchmeier.capsule" version "1.0.2"
	id "com.diffplug.gradle.spotless" version "1.3.3"
	id "org.sonarqube" version "2.5"
}

apply plugin: 'java'
apply from: "gradle/git.gradle"
apply plugin: 'checkstyle'
apply plugin: "com.diffplug.gradle.spotless"
apply plugin: 'jacoco'

ext.mainClass = 'moviescraper.doctord.Main'
version = getVersionCode()

repositories {
	mavenCentral()
	maven { url "http://clojars.org/repo/" }
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
		}
		resources {
			srcDirs = ['src/main/resources']
		}
	}
}
dependencies {
	compile (
		'commons-io:commons-io:2.4',
		'org.apache.commons:commons-lang3:3.1',
		'commons-cli:commons-cli:1.2',
		'commons-codec:commons-codec:1.8',
		'org.apache.commons:commons-csv:1.1',
		'com.jgoodies:jgoodies-common:1.4.0',
		'com.jgoodies:jgoodies-forms:1.6.0',
		'org.imgscalr:imgscalr-lib:4.2',
		'com.cedarsoftware:json-io:4.3.0',
		'org.jsoup:jsoup:1.7.3',
		'com.thoughtworks.xstream:xstream:1.4.7',
		'org.json:json:20151123',
		'org.hamcrest:hamcrest-core:1.3',
		'xpp3:xpp3_min:1.1.4c',
		'xmlpull:xmlpull:1.1.3.1',
		'com.jgoodies:forms:1.3.0',
		'org.apache.httpcomponents:httpclient:4.5.3'
	)
	testCompile('junit:junit:4.+')
}

jar {

	into 'resources', {
		from 'resources'
	}
	manifest {
		attributes(
			"Implementation-Title": "Gradle",
			"Implementation-Version": version,
			"Main-Class" : mainClass
		)
	}
}

test {
	testLogging {
		outputs.upToDateWhen {false}
		showStandardStreams = true
	}
}

task dist(type: FatCapsule) {
	applicationClass mainClass
	archiveName project.name + ".jar"
	capsuleManifest {
		systemProperties['log4j.configuration'] = 'log4j.xml'
		jvmArgs = ['-Xms768m', '-Xmx1g']
		minJavaVersion = '1.6.0'
	}
}

spotless {
	java {
		eclipseFormatFile 'config/format/eclipseformat.xml'
	}
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

apply from: "gradle/codacy_coverage.gradle"
